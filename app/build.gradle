//框架开发模式-需要测试
//apply plugin: 'com.android.application'
apply plugin: 'android-library'
android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            // 支持自动去除没有使用的资源 默认关闭
            minifyEnabled false
            shrinkResources false
            //混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}
// 1. test-plugin
apply plugin: 'robolectric'
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // 2. test-libs
    androidTestCompile 'junit:junit:4.12'
    //框架开发模式-需要测试
    //    androidTestCompile 'com.squareup:fest-android:1.0.8'
    androidTestCompile 'org.robolectric:robolectric:2.4'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.jakewharton:butterknife:6.1.0'
    //noinspection GradleCompatible
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.squareup.dagger:dagger-compiler:1.2.2'
}
// 3. test settings
robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    // configure max heap size of the test JVM
    maxHeapSize = "2048m"
}
// 1.maven-插件
apply plugin: 'maven'
// 2.maven-信息
ext {
    PUBLISH_GROUP_ID = 'j2w.team'
    PUBLISH_ARTIFACT_ID = 'base'
    PUBLISH_VERSION = android.defaultConfig.versionName
}
// 3.maven-路径
uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file(getProperty('aar.deployPath'))
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId project.PUBLISH_GROUP_ID
            artifactId project.PUBLISH_ARTIFACT_ID
            version project.PUBLISH_VERSION
        }
    }
}
